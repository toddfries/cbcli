#!/usr/bin/perl

# Copyright (c) 2014,2017 Todd T. Fries <todd@fries.net>
#
# Permission to use, copy, modify, and distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.

use strict;
use warnings;

use Data::Dumper;
use Getopt::Std;
use LWP::Authen::OAuth2;
use JSON;

our $cbversion = "2017-01-09";

our $json = JSON->new->allow_nonref;

our $opt_v = 0;
our $opt_c = "$ENV{'HOME'}/.coinbase.conf";
our $opt_l;
getopts('c:l:v');

my ($token_string,$id,$secret,$token_refresh);
my $conf = $opt_c;

open(N,$conf);
chomp($id = <N>);
chomp($secret = <N>);
my $tmpstr = <N>;
if (defined($tmpstr)) {
	chomp($token_string = $tmpstr);
	$tmpstr = <N>;
}
close(N);

our $urloabase = 'https://coinbase.com/oauth';
our $urlapibase = 'https://api.coinbase.com/v2';
our $authurl = $urloabase . '/authorize';
our $tokenurl = $urloabase . '/token';
our $buttonurl = $urlapibase . '/buttons';

our $oobredir = 'urn:ietf:wg:oauth:2.0:oob';

my $randstate = "";
my $i=0;
while ($i++ < 10) {
	$randstate .= sprintf "%x", int(rand(256));
}

my $oauth2 = LWP::Authen::OAuth2->new(
	client_id => $id,
	client_secret => $secret,

	# have to create LWP::Authen::OAuth2::CoinBase
	#service_provider => 'CoinBase',
	# - vs - 
	authorization_endpoint => $authurl,
	token_endpoint => $tokenurl."?redirect_uri=$oobredir",

	redirect_uri => $oobredir,

	save_tokens => \&save_tokens,
	save_tokens_args => [ $id, $secret ],

	token_string => $token_string,
);

my @scopes = (
	"wallet:user:read",
	"wallet:accounts:read",
	"wallet:transactions:read",
);

if (!defined($token_string)) {
	my $scope;
	foreach my $s (@scopes) {
		if (defined($scope)) {
			$scope .= ",".$s;
		} else {
			$scope = $s;
		}
	}
	my $url = $oauth2->authorization_url(
		response_type=> 'code',
		client_id => $id,
		state => $randstate,
		scope => $scope,
	);
	$url .= "&account=all";

	if (defined($ENV{'DISPLAY'}) && -f "/usr/local/bin/firefox") {
		system("/usr/local/bin/firefox \"$url\"");
	} else {
		printf "Go visit this url: %s\n", $url;
	}
	print "Enter code: ";
	my $code;
	chomp($code = <stdin>);

	my $res;
	eval { $res = $oauth2->request_tokens(
			grant_type => 'authorization_code',
			code => $code,
			client_id => $id,
			client_secret => $secret,
		);
	};
	if ($@) {
		print STDERR "request_tokens: ".$@."\n";
		exit(1);
	}

	#print(Dumper $res);
	print "\n";
}

$token_string = $oauth2->token_string;

#printf "Token string: %s\n", $token_string;

if ($oauth2->can_refresh_tokens()) {
	if ($opt_v > 0) {
		print "Seems possible to refresh tokens!\n";
	}
	my $ret = $oauth2->should_refresh();
	if ($opt_v > 0) {
		if ($ret) {
			print "We shold refresh our tokens..\n";
		} else {
			print "No need to refresh tokens yet.\n";
		}
	}
} else {
	print "Looks not possible to refresh tokens..\n";
}


my $res;
# XXX check before we fail below?
#$res = $oauth2->get($urlapibase . '/user/auth', %headers);

my $limit = 2;     # 25 (default), 0 - 100
my $order = "asc"; # desc (newest 1st, default), asc (oldest 1st)

my @accts;

my @resp = get($urlapibase . '/accounts');
foreach my $parsed (@resp) {
	if (defined($parsed->{data})) {
		foreach my $k ($parsed->{data}) {
			if (ref($k) eq "HASH") {
				foreach my $l (keys %{$k}) {
					printf "data: %s : %s\n", $l, $k->{$l};
				}
				next;
			}
			if (ref($k) eq "ARRAY") {
				foreach my $l (@{$k}) {
					#printf "data: found a %s\n", ref($l);
					push @accts,$l;
				}
				next;
			}
			printf "data: element is a %s\n", ref($k);
			$i++;
		}
	}
}

# XXX totals, but must account for different currencies,
# looking for total in btc then conversion to usd of course
our %seen;
my $acctcount=0;
my $nativetotal=0.0;
my $nativename="";
foreach my $a (sort order_accts @accts) {
	show_acct($a);
	$nativetotal+= $a->{native_balance}->{amount};
	$nativename = $a->{native_balance}->{currency};
	$acctcount++;
	if (defined($opt_l) && $a->{name} eq $opt_l) {
		list_acct($a);
	}
}

printf "Processed %d accounts ... total %s %s\n", $acctcount, $nativetotal, $nativename;

1;

sub list_acct {
	my ($acct) = @_;
	my @resp = get($urlapibase . '/accounts/' . $acct->{id} . '/transactions');
	print Dumper([@resp]);
}

sub get {
	my ($url) = @_;

	print "get('$url') called\n";

	my %headers;
	$headers{'CB-VERSION'}=$cbversion;

	my @responses;
	my $last_cursor_pos = 0;
	my $last_cursor_id;

    while(1) {
	if ($opt_v > 0) {
		printf "Starting round after %d items", $last_cursor_pos;
		if (defined($last_cursor_id)) {
			printf " after id %s", $last_cursor_id;
		}
		print "\n";
	}

	my $parms = "?limit=${limit}&order=${order}";
	if (defined($last_cursor_id)) {
		$parms.="&starting_after=${last_cursor_id}";
	}

	$res = $oauth2->get(${url}.${parms}, %headers);
	if (!defined($res)) {
		die "get ${url}${parms} failed!";
	}
	if (! $res->is_success) {
		print Dumper($res);
		die $res->status_line;
	}

	my $parsed = parse_json($res->decoded_content, 'GET ${url}${parms}');
	#printf "parsed is a %s\n", $parsed;
	push @responses, $parsed;

	if ($opt_v > 0) {
		if (defined($parsed->{pagination})) {
			foreach my $k (keys %{$parsed->{pagination}}) {
				my $val = $parsed->{pagination}->{$k};
				if (!defined($val)) {
					$val = "</dev/null>";
				}
				printf "pagination %s : %s\n", $k, $val;
			}
		}
	}
	if (defined($parsed->{data})) {
		foreach my $k ($parsed->{data}) {
			if (ref($k) eq "ARRAY") {
				foreach my $l (@{$k}) {
					$last_cursor_pos++;
					$last_cursor_id=$l->{id};
				}
				next;
			}
		}
	}

	if (!defined($parsed->{pagination}->{next_uri})) {
		last;
	}
    }
    return @responses;
}

sub save_tokens {
	my ($token_string,$id,$secret) = @_;
	my $parsed = parse_json($token_string, 'token_string');

	open(T,">$conf");
	foreach my $line (($id,$secret,$token_string)) {
		print T $line."\n";
	}
	close(T);
}

sub parse_json {
	my ($str,$name) = @_;

	my $parsed;
	eval {
		$parsed = $json->decode( $str );
	};
	if ($@) {
		die("%s: json->decode('%s') Error %s\n", $name, $str, $@);
		return undef;
	}
	if ($opt_v > 0) {
		printf "Pretty %s: %s\n", $name, $json->pretty->encode( $parsed )."\n";;
	}
	return $parsed;
}

sub show_acct {
	my ($acct) = @_;
	if (defined($seen{$acct->{id}})) {
		return;
	}
	$seen{$acct->{id}}=1;
	printf "%-26s: %12.8f %s = %8.2f %s (%s)\n",
	    $acct->{name},
	    $acct->{balance}->{amount},
	    $acct->{balance}->{currency},
	    $acct->{native_balance}->{amount},
	    $acct->{native_balance}->{currency},
	    $acct->{type};
}

sub order_accts {
	my $res;
	$res = $a->{type} cmp $b->{type};
	if ($res != 0) {
		return $res*-1;
	}
	$res = $a->{currency} cmp $b->{currency};
	if ($res != 0) {
		return $res;
	}
	return $a->{name} cmp $b->{name};
}

package WebService::Coinbase2;
use Moose;
with 'WebService::Client';

use Crypt::Mac::HMAC qw(hmac hmac_hex);
use Function::Parameters;
use HTTP::Request::Common qw(DELETE GET POST PUT);
use Time::HiRes qw(time);

has client_id => (
    is       => 'ro',
    required => 1,
);

has client_secret => (
    is       => 'ro',
    required => 1,
);

has '+base_url' => (
    is      => 'ro',
    default => 'https://api.coinbase.com/v2',
);

sub BUILD {
    my ($self) = @_;
    $self->ua->default_header(':ACCESS_KEY' => $self->api_key);
}

around req => fun($orig, $self, $req, @rest) {
    my $nonce = time * 1e5;
    my $signature =
        hmac_hex 'SHA256', $self->api_secret, $nonce, $req->uri, $req->content;
    $req->header(':ACCESS_NONCE'     => $nonce);
    $req->header(':ACCESS_SIGNATURE' => $signature);
    return $self->$orig($req, @rest);
};

method get_accounts { $self->get('/accounts') }

method get_account($id) { $self->get("/accounts/$id") }

method create_account(HashRef $data) {
    return $self->post("/accounts", { account => $data });
}

method modify_account($id, HashRef $data) {
    return $self->put("/accounts/$id", { account => $data });
}

method get_balance { $self->get('/account/balance') }

method get_account_balance($id) { $self->get("/accounts/$id/balance") }

method get_account_address($id) { $self->get("/accounts/$id/address") }

method create_account_address($id, HashRef $data) {
    return $self->post("/accounts/$id/address", { address => $data });
}

method get_primary_account { $self->get("/accounts/primary") }

method set_primary_account($id) { $self->post("/accounts/$id/primary") }

method delete_account($id) { $self->delete("/accounts/$id") }

method get_addresses { $self->get("/addresses") }

method get_address($id) { $self->get("/addresses/$id") }

method get_contacts { $self->get('/contacts') }

method get_transactions { $self->get('/transactions') }

method get_transaction($id) { $self->get("/transactions/$id") }

method send_money($data) {
    return $self->post('/transactions/send_money', { transaction => $data });
}

method transfer_money($data) {
    return $self->post('/transactions/transfer_money',{ transaction => $data });
}

method request_money($data) {
    return $self->post('/transactions/request_money', { transaction => $data });
}

method resend_request($id) {
    return $self->put("/transactions/$id/resend_request");
}

method complete_request($id) {
    return $self->put("/transactions/$id/complete_request");
}

method cancel_request($id) {
    return $self->put("/transactions/$id/cancel_request");
}

method get_buy_price(Maybe[HashRef] :$query) {
    return $self->get("/prices/buy", $query);
}

method get_sell_price(Maybe[HashRef] :$query) {
    return $self->get("/prices/sell", $query);
}

method get_spot_price(Maybe[HashRef] :$query) {
    return $self->get("/prices/spot_rate", $query);
}

method get_orders { $self->get('/orders') }

method create_order(HashRef $data) {
    return $self->post('/orders', { button => $data });
}

method get_order($id) { $self->get("/orders/$id") }

method refund_order($id, HashRef $data) {
    return $self->post("/orders/$id", { order => $data });
}

# ABSTRACT: Coinbase (http://coinbase.com) API bindings


1;

__END__

=pod

=encoding UTF-8

=head1 NAME

WebService::Coinbase - Coinbase (http://coinbase.com) API bindings

=head1 VERSION

version 0.0200

=head1 SYNOPSIS

    use WebService::Coinbase;

    my $coin = WebService::Coinbase->new(
        api_key    => 'API_KEY',
        api_secret => 'API_SECRET',
        logger     => Log::Tiny->new('/tmp/coin.log'), # optional
    );
    my $accounts = $coin->get_accounts();

=head1 METHODS

=head2 get_accounts

    get_accounts()

Returns the user's active accounts.

=head2 get_account

    get_account($account_id)

Returns one of the user's active accounts.

=head2 get_primary_account

    get_primary_account()

Returns the user's primary account.

=head2 set_primary_account

    set_primary_account($account_id)

Sets the primary account.

=head2 create_account

    create_account($data)

Creates a new account for the user.

Example:

    my $account = $coin->create_account({ name => "Bling Bling" });

=head2 get_balance

    get_balance()

Returns the user's current balance.

=head2 get_account_balance

    get_account_balance($account_id)

Returns the current balance for the given account.

=head2 get_account_address

    get_account_address($account_id)

Returns the user's current bitcoin receive address.

=head2 create_account_address

    create_account_address($account_id, $data)

Generates a new bitcoin receive address for the user.

Example:

    $coin->create_account_address($account_id, {
        label        => 'college fund',
        callback_url => 'http://foo.com/bar',
    });

=head2 modify_account

    modify_account($account_id, $data)

Modifies an account.

Example:

    $coin->modify_account($acct_id, { name => "Kanye's Account" });

=head2 delete_account

    delete_account($account_id)

Deletes an account.
Only non-primary accounts with zero balance can be deleted.

=head2 get_addresses

    get_addresses()

Returns the bitcoin addresses a user has associated with their account.

=head2 get_address

    get_address($id_or_address)

Returns the bitcoin address object for the given id or address.

=head2 get_contacts

    get_contacts()

Returns contacts the user has previously sent to or received from.

=head2 get_transactions

    get_transactions()

Returns the user's transactions sorted by created_at in descending order.

=head2 get_transaction

    get_transaction($transaction_id)

Returns the transaction for the given id.

=head2 send_money

    send_money($data)

Send money to an email or bitcoin address.

Example:

    $coin->send_money({
        to       => $email_or_bitcoin_address,
        amount   => '1.23',
        notes    => 'Here is your money',
    });

=head2 transfer_money

    transfer_money($data)

Transfer bitcoin between accounts.

=head2 request_money

    request_money($data)

Request money from an email.

=head2 resend_request

    resend_request($transaction_id)

Resend a money request.

=head2 complete_request

    complete_request($transaction_id)

Lets the recipient of a money request complete the request by sending money to
the user who requested the money.
This can only be completed by the user to whom the request was made,
not the user who sent the request.

=head2 cancel_request

    cancel_request($transaction_id)

Cancel a money request.

=head2 get_buy_price

    get_buy_price()
    get_buy_price(query => { qty => 1 })

=head2 get_sell_price

    get_sell_price()
    get_sell_price(query => { qty => 1 })

=head2 get_spot_price

    get_spot_price()
    get_spot_price(query => { currency  => 'CAD' })

=head2 get_orders

    get_orders()

Returns a merchant's orders that they have received.

=head2 create_order

    create_order($data)

Returns an order for a new button.

Example:

    $coin->create_order({
        name               => 'test',
        price_string       => '1.23',
        price_currency_iso => 'BTC',
    });

=head2 get_order

    get_order($order_id)

Returns order details.

=head2 refund_order

    refund_order($order_id, $data)

Refunds an order.

Example:

    $coin->refund_order($order_id, { refund_iso_code => 'BTC' })

=head1 AUTHOR

Naveed Massjouni <naveed@vt.edu>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2015 by Naveed Massjouni.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
